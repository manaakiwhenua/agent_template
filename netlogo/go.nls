

to go
  ;; This function is called every iteration
  ;; 
  ;; setup now if never setup
  if (stop-after-year = 0) [setup]
  ;; begin "go" output
  if _first-go [
    output-print "go:"
    set _first-go false
  ]
  output-print (word "  - year: " ticks)
  apply-rules
  make-farmer-choices
  update-derived-model-quantities
  update-display
  ;; increase land use age
  ask valid-patches [set landuse-age (landuse-age + 1)]
  ;; step time and maybe stop model
  tick
  if ticks >= stop-after-year [
    set stop-after-year ( stop-after-year + years-to-run-before-stopping )
  stop ]
end

to apply-rules
  ;; initialise options to choose from
  ask farmers [ set landuse-option-weights (n-values (length landuse-code) [0]) ]
  ;; execute rules, adding to landuse-option-weights that are chosen from
  ;; below
  if Baseline [apply-baseline-rule]
  if Neighbourhood [apply-neighbour-rule]
  if Network [apply-network-rule]
  if Industry-level [apply-industry-level-rule]
  if Government-level [apply-government-level-rule]
  if economy-rule [apply-economy-rule]
  if emissions-rule [apply-emissions-rule]
end

to make-farmer-choices
  ;; make new land use choice based on landuse-option-weights
  ;;
  ask farmers [
    ;; if not decision interval -- do nothing
    if (landuse-age mod decision-interval ) = 0 [
      ;; if no weight on any new option then do nothing
      if sum landuse-option-weights > 0 [
        ;; weighted random choice
        let LU-new (choose landuse-code landuse-option-weights)
        ;; if different from the current landuse this is a change
        if LU-new != LU [
          set LU LU-new
          set landuse-age 0]]]]
end

to update-derived-model-quantities
  ;; Calculate patch and global quantities that depende on land use
  ;; 
  ;; Compute crop yields
  ask valid-patches [set crop-yield item LU landuse-crop-yield]
  ;; compute livestock yields
  ask valid-patches [set livestock-yield item LU landuse-livestock-yield]
  ;; compute carbon stock
  ask valid-patches [set carbon-stock (min (list
    (landuse-age * (item LU landuse-carbon-stock-rate))
    (item LU landuse-carbon-stock-maximum)))]
  ;; compute Shannon index of diversity, do not include missing value
  ;; land use
  let total-number-of-valid-patches (count valid-patches)
  set diversity-index 0
  foreach landuse-code [ this-LU ->
    let p ( (count valid-patches with [ LU = this-LU ]) / total-number-of-valid-patches )
    if ( p > 0) [
    set diversity-index (diversity-index + (-1 * p * (ln p)))]]
  ;; compute contiguity index
  ;;
  ;; ref URL from Clemence https://www.fragstats.org/index.php/fragstats-metrics/patch-based-metrics/shape-metrics/p5-contiguity-index
  ;;
  ;; example code from Clemence.
  ;;
  ;; let contiguity-index 0
  ;; ask patches [
  ;;   let neighbors-with-same-value neighbors with [my-value = [my-value] of myself]
  ;;   (ifelse any? neighbors-with-same-value
  ;;   [let weighted-contiguity sum [1 / distance myself] of neighbors-with-same-value
  ;;    set contiguity-index contiguity-index + weighted-contiguity]
  ;;    ;; Handle case when there are no neighbors with the same value
  ;;   [set contiguity-index contiguity-index + 0]
  ;; )]
  ;;
  ;; My code. Why distance since finding direct neighbours? How to
  ;; normalise the index?
  set contiguity-index 0
  ask valid-patches [
    ask neighbors with [LU = [LU] of myself] [
        set contiguity-index (contiguity-index + (1 / distance myself))]]
  ;; pollination index: Clemence's explanation: Simplest way consists
  ;; in analysing the presence of scrub cell within the neighbourhood
  ;; (500m = 4cells) of a crop patch (perennial or annual). Report 1
  ;; if yes and 0 if no. Add the number of cells=1 and divide by the
  ;; total number of crop cells (annual and perennial)
  ask valid-patches [set pollinated 0]
  ask valid-patches with [(LU = 3) or (LU = 4)] [
    if (count valid-patches with [((distance myself) <= 4) and (LU = 5)] >= 1) [
        set pollinated 1]]
  set pollination-index (sum [pollinated] of valid-patches)
  if (pollination-index > 0) [
    set pollination-index (pollination-index / count valid-patches with [(LU = 3) or (LU = 4)])]
  ;; bird suitability index: Clemence's explanation Concerns the
  ;; perennial crops and forest (exotic+natural) cells. Value= the
  ;; number of cells where the habitat quality is ok for native birds
  ;; (like Kereru) / total number of cells. Simplest way consists in
  ;; analysing all concerns cells: is this cell surrounding by at
  ;; least 19 patches of LU 4, 8 or 9 ? Report 1 if yes and 0 if
  ;; no. Add the number of cells=1 and divide by the total number of
  ;; cells.
  ask valid-patches [set bird-suitable 0]
  ask valid-patches with [(LU = 4) or (LU = 8) or (LU = 9)] [
  if (count valid-patches with [((distance myself) <= 4) and ((LU = 4) or (LU = 8) or (LU = 9))] >= 19) [
        set bird-suitable 1]]
  set bird-suitability-index ((sum [bird-suitable] of valid-patches) / (world-size ^ 2))
  ;; compute CO2 equivalent emissions
  ask valid-patches [set emissions item LU landuse-emissions]
  set previous-emissions total-emissions
  set total-emissions sum [emissions] of valid-patches
  ;; compute gross margin values per LU (ref Herzig et al) for each
  ;; patch, and compute the total
  ask valid-patches [
    set value (ifelse-value
    ;; Artificial: 300,000$/ha when agricultural land is converted
    ;; into artificial. Itâ€™s a one-off.
    (LU = 1) [ifelse-value (landuse-age = 0) [300000] [0] ]
    ;; Water: 0 yield and 0$
    (LU = 2) [0]
    ;; Annual crops: 10t/ha (yield), 450$/t
    (LU = 3) [450 * crop-yield]
    ;; Perennial crops: 20t/ha (yield), 2500$/t
    (LU = 4) [3500 * crop-yield]
    ;; Intensive pasture: 1.1 t/ha (yield), 10,000$/t
    (LU = 5) [10000 * livestock-yield]
    ;; Extensive pasture: 0.3 t/ha (yield), 5,500$/t
    (LU = 6) [5500 * livestock-yield]
    ;; Scrub 0,0
    (LU = 7) [0]
    ;; Natural forest 0,0
    (LU = 8) [0]
    ;; Exotic forest: 4500$/ha
    (LU = 9) [4500]
    ;; should never occur
    [-99999999])]
  set previous-total-value total-value
  set total-value sum [value] of valid-patches
  ;; print some things in the output box
  output-print (word "    total-value: " total-value)
  output-print (word "    total-emissions: " total-emissions)
end
