;; This file contains the encoded agent decision rules.  Each function
;; takes no arguments and adds weight to farmer's land use options

to apply-baseline-rule
  ;; execute basic rule
  ;; 
  ask deciding-farmers [
    ;;will continue to trigger behaviour after 30 iterations.
    (ifelse
      (behaviour = 2) [(ifelse
        (LU = 3) [add-weight-to-landuse-option one-of [3 4 6] baseline-rule-weight]
        (LU = 6) [add-weight-to-landuse-option one-of [3 4 6] baseline-rule-weight]
        (LU = 7) [add-weight-to-landuse-option one-of [7 9] baseline-rule-weight]
        (LU = 9) [add-weight-to-landuse-option one-of [7 9] baseline-rule-weight]
        [do-nothing])]
      (behaviour = 3) [(ifelse
        (LU = 3) [add-weight-to-landuse-option one-of [3 4] baseline-rule-weight]
        (LU = 4) [add-weight-to-landuse-option one-of [4 8] baseline-rule-weight]
        (LU = 6) [add-weight-to-landuse-option one-of [4 3] baseline-rule-weight]
        (LU = 7) [add-weight-to-landuse-option one-of [7 8 9] baseline-rule-weight]
        (LU = 9) [add-weight-to-landuse-option one-of [9 8 7] baseline-rule-weight]
        [do-nothing])]
      [do-nothing])]
end

to apply-neighbour-rule
  ;; execute neighbour rule
  ;; 
  ask deciding-farmers [
  ;; Compute a list counting neighbours of this farmer (not
    ;; including self) with particular land uses
    let count-LU
      map [this-LU -> count neighbour-link-neighbors with [LU = this-LU]]
      landuse-code
    ;; Determine which landuse-code has the highest frequency among
    ;; neighbours. If it is a tie, then is it the first or random LU
    ;; that is selected?
    let most-common-LU (item (position (max count-LU) count-LU) landuse-code)
    (ifelse
      (behaviour = 1) [
         if (member? LU [3 4 6 7 9]) and most-common-LU = 1
       [add-weight-to-landuse-option 1 neighbour-rule-weight]]
      (behaviour = 2) [
        add-weight-to-landuse-option (ifelse-value
          ((member? LU [3 4 6 7 9]) and most-common-LU = 1) [1]
          ((member? LU [3 6 7])     and most-common-LU = 3) [3]
          ((member? LU [3 4 6 7])   and most-common-LU = 4) [4]
          ((member? LU [3 4 6 7])     and most-common-LU = 6) [6]
          ((member? LU [3 7 9])     and most-common-LU = 7) [7]
          ((member? LU [3 7 9])     and most-common-LU = 9) [9]
          [LU]) neighbour-rule-weight]
      (behaviour = 3) [
          add-weight-to-landuse-option (ifelse-value
            ((member? LU [3 6 7])   and most-common-LU = 3) [3]
            ((member? LU [3 4 6 7]) and most-common-LU = 4) [4]
            ((member? LU [3 6])     and most-common-LU = 7) [7]
            ((member? LU [3 7])     and most-common-LU = 9) [9]
            (LU != 8 and LU != 1    and most-common-LU = 8) [8]
            [LU]) neighbour-rule-weight]
      [do-nothing]       ;actually should never happen because only 3 behaviours, but require an else clause
    )]
end

to apply-network-rule
  ;; compute network most common land use for all networks
  ask landuse-networks [
    ;; count land uses in this network
    let landuse-counts
        map [this-LU -> count my-landuse-network-links with [[LU] of other-end = this-LU]]
        landuse-code
    ;; find the most common land use
    let max-landuse-count-index position (max landuse-counts) landuse-counts
    set most-common-landuse item max-landuse-count-index landuse-code
    let this-most-common-landuse most-common-landuse
    ;; inform famers in the network, gross use of myself due to nested
    ;; ask statements
    ask my-landuse-network-links [
      ask other-end [set most-common-landuse-in-network this-most-common-landuse]]]
  ;; farmer decision
  ask deciding-farmers [
    let most-common-LU most-common-landuse-in-network
    (ifelse
      (behaviour = 1) [
         if (member? LU [3 4 6 7 9]) and most-common-LU = 1
       [add-weight-to-landuse-option 1 neighbour-rule-weight]]
      (behaviour = 2) [
        add-weight-to-landuse-option (ifelse-value
          ((member? LU [3 4 6 7 9]) and most-common-LU = 1) [1]
          ((member? LU [3 6 7])     and most-common-LU = 3) [3]
          ((member? LU [3 4 6 7])   and most-common-LU = 4) [4]
          ((member? LU [3 4 6 7])     and most-common-LU = 6) [6]
          ((member? LU [3 7 9])     and most-common-LU = 7) [7]
          ((member? LU [3 7 9])     and most-common-LU = 9) [9]
          [LU]) neighbour-rule-weight]
      (behaviour = 3) [
          add-weight-to-landuse-option (ifelse-value
            ((member? LU [3 6 7])   and most-common-LU = 3) [3]
            ((member? LU [3 4 6 7]) and most-common-LU = 4) [4]
            ((member? LU [3 6])     and most-common-LU = 7) [7]
            ((member? LU [3 7])     and most-common-LU = 9) [9]
            (LU != 8 and LU != 1    and most-common-LU = 8) [8]
            [LU]) neighbour-rule-weight]
      [do-nothing]       ;actually should never happen because only 3 behaviours, but require an else clause
    )]
end

to apply-industry-level-rule
  if total-value < previous-total-value [
    ask n-of (5 * count valid-patches with [LU = 3 ] / 100) valid-patches [set LU one-of [4 6]]
    ask n-of (5 * count valid-patches with [LU = 6 ] / 100) valid-patches [set LU one-of [4 ]]
    ask n-of (5 * count valid-patches with [LU = 7 ] / 100) valid-patches [set LU one-of [3 4 6]]]
end

to apply-government-level-rule
   if total-emissions > previous-emissions [
     ask n-of (10 * count valid-patches with [LU = 6 ] / 100) valid-patches [set LU one-of [3 4]]
     ask n-of (10 * count valid-patches with [LU = 7 ] / 100) valid-patches [set LU one-of [9]]]
end

to apply-economy-rule
  if total-value < previous-total-value [
    ask deciding-farmers with [LU = 3] [add-weight-to-landuse-option (one-of [4 6]) economy-rule-weight]
    ask deciding-farmers with [LU = 6] [add-weight-to-landuse-option 4 economy-rule-weight]
    ask deciding-farmers with [LU = 7] [add-weight-to-landuse-option (one-of [3 4 6]) economy-rule-weight]]
end

to apply-emissions-rule
   if total-emissions > previous-emissions [
      ask deciding-farmers with [LU = 6] [add-weight-to-landuse-option (one-of [3 4]) emissions-rule-weight]
      ask deciding-farmers with [LU = 7] [add-weight-to-landuse-option (one-of [9]) emissions-rule-weight]]
end
